# ============================================================================
# INSTANT PROMPT (Must be at the very top)
# ============================================================================

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ============================================================================
# ENVIRONMENT VARIABLES
# ============================================================================

if [[ -x $(command -v brew) ]]; then
  BREW_PREFIX=$(brew --prefix)
elif [[ -x /usr/local/bin/brew ]]; then
  BREW_PREFIX="/usr/local"
elif [[ -x /opt/homebrew/bin/brew ]]; then
  BREW_PREFIX="/opt/homebrew"
else
  BREW_PREFIX=""
fi

if [[ -n "$BREW_PREFIX" ]]; then
  export PATH="$BREW_PREFIX/share/john:$PATH"
  export PATH="$BREW_PREFIX/opt/postgresql@18/bin:$PATH"
  export LDFLAGS="-L$BREW_PREFIX/opt/postgresql@18/lib"
  export CPPFLAGS="-I$BREW_PREFIX/opt/postgresql@18/include"
fi

if [[ -x $(command -v nvim) ]]; then
  export EDITOR="nvim"
elif [[ -x $(command -v vim) ]]; then
  export EDITOR="vim"
else
  export EDITOR="vi"
fi

export ZSH_TMUX_CONFIG="$HOME/.config/tmux/tmux.conf"

# ============================================================================
# OH-MY-ZSH CONFIGURATION
# ============================================================================

export ZSH="$HOME/.oh-my-zsh"
ZSH_THEME="powerlevel10k/powerlevel10k"

# Plugin-specific settings (before sourcing oh-my-zsh)
zstyle ':omz:plugins:alias-finder' autoload yes
zstyle ':omz:plugins:alias-finder' exact yes
zstyle ':omz:plugins:eza' 'color-scale' all
zstyle ':omz:plugins:eza' 'dirs-first' yes
zstyle ':omz:plugins:eza' 'git-status' yes
zstyle ':omz:plugins:eza' 'header' yes
zstyle ':omz:plugins:eza' 'icons' yes

ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=#636DA6"
ZSH_AUTOSUGGEST_STRATEGY=completion

plugins=(
  alias-finder
  aliases
  azure
  colored-man-pages
  command-not-found
  docker
  docker-compose
  emoji
  eza
  gh
  git
  helm
  jsontools
  kubectl
  minikube
  npm
  pip
  python
  rails
  rake
  ruby
  tailscale
  tmux
  web-search
  yarn
  z
  zsh-autopair
  zsh-autosuggestions
  zsh-history-substring-search
  zsh-syntax-highlighting
  zsh-vi-mode
)

source $ZSH/oh-my-zsh.sh

# ============================================================================
# ZSH OPTIONS & VI MODE CONFIGURATION
# ============================================================================

insert-linebreak () {
  LBUFFER+=\ \\$'\n'
}
zle -N insert-linebreak

bindkey -v
bindkey '^x' clear-screen
bindkey '^h' vi-backward-char
bindkey '^l' vi-forward-char
bindkey "^[^M" insert-linebreak
bindkey -M vicmd ';' edit-command-line
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# Zsh Vi Mode colors
ZVM_VI_HIGHLIGHT_FOREGROUND=#82AAFF
ZVM_VI_HIGHLIGHT_BACKGROUND=#636DA6

# ============================================================================
# ALIASES
# ============================================================================

unalias ya 2>/dev/null

alias vim="nvim"
e() {
  "$EDITOR" "$@"
}

if [[ -x $(command -v bat) ]]; then
  alias cat="bat --theme=Dracula"
elif [[ -x $(command -v batcat) ]]; then
  alias cat="batcat"
fi

alias ll='ls -la --total-size'
alias la='ls -a'
alias lt='ls --tree'

alias j="just"
alias jl="just --list"
alias ju="just update"

alias d="lazydocker"
alias g="lazygit"
alias y="yadm enter lazygit"

alias watch="viddy -d -n 1 --shell zsh "$@""

alias lynx="lynx -cfg=$HOME/.config/lynx/lynx.cfg -lss=$HOME/.config/lynx/lynx.lss"

if [ "$(yadm config local.class)" = "Personal" ]; then
  alias vpnify="/Applications/Vpnify.app/Contents/MacOS/CLI/vpnify"
fi

if [ "$(yadm config local.class)" = "42" ]; then
  alias cclean='bash ~/LinuxCleaner_42.sh'
fi

# Yazi with directory change on exit
x() {
  local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
  yazi "$@" --cwd-file="$tmp"
  if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
    builtin cd -- "$cwd"
  fi
  rm -f -- "$tmp"
}

# Z with fzf integration
zi() {
  if ! command -v z &>/dev/null; then
    echo "z not found"
    return 1
  fi
  local dir
  dir=$(z | awk '{$1=""; sub(/^ +/, ""); print }' | fzf --tac --preview 'eza -gh --git --icons=auto --color=always --color-scale=all --tree "$(echo {})" | head -80')
  [[ -n "$dir" ]] && cd "$dir"
}

# VisiData pip wrapper
vdpip() {
  if ! command -v visidata &>/dev/null; then
    echo "Visidata not found."
    return 1
  fi

  local vdp_python
  if [[ -n "$BREW_PREFIX" ]]; then
    vdp_python="$BREW_PREFIX/opt/visidata/libexec/bin/python3"
  else
    vdp_python="$(command -v visidata | xargs dirname)/../libexec/bin/python3"
  fi

  if [[ ! -x "$vdp_python" ]]; then
    echo "Embedded Python not found at: $vdp_python"
    return 1
  fi

  "$vdp_python" -m pip "$@"
}

# ============================================================================
# LOAD ITERM2 SHELL INTEGRATION
# ============================================================================

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# ============================================================================
# PROMPT CONFIGURATION
# ============================================================================

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# ============================================================================
# SOURCE ADDITIONAL SCRIPTS
# ============================================================================

scripts_dir="${0:A:h}/zshrc.os"

if [[ -d "$scripts_dir" ]]; then
  setopt null_glob
  for script in "$scripts_dir"/*.sh; do
    [[ "$script" == *"##"* ]] && continue
    [[ -r "$script" ]] && source "$script"
  done
  unsetopt null_glob
fi
