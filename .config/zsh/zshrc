# ============================================================================
# FASTFETCH (displays system info on startup)
# ============================================================================

if [[ -o interactive ]] && [[ -z "$_ZSHRC_SOURCED" ]]; then
  if (( $+commands[fastfetch] )); then
    fastfetch
  fi
  export _ZSHRC_SOURCED=1
fi

# ============================================================================
# INSTANT PROMPT (Anything below will run in the background)
# ============================================================================

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ============================================================================
# ENVIRONMENT VARIABLES
# ============================================================================

if [[ -x /opt/homebrew/bin/brew ]]; then
  BREW_PREFIX="/opt/homebrew"
elif [[ -x /usr/local/bin/brew ]]; then
  BREW_PREFIX="/usr/local"
else
  BREW_PREFIX=""
fi

typeset -U PATH path

if [[ -n "$BREW_PREFIX" ]]; then
  path=(
    "$BREW_PREFIX/opt/postgresql@18/bin"
    "$BREW_PREFIX/share/john"
    $path
  )
  export LDFLAGS="-L$BREW_PREFIX/opt/postgresql@18/lib"
  export CPPFLAGS="-I$BREW_PREFIX/opt/postgresql@18/include"
fi

path+=("$HOME/.local/bin")

if (( $+commands[nvim] )); then
  export EDITOR="nvim"
elif (( $+commands[vim] )); then
  export EDITOR="vim"
else
  export EDITOR="vi"
fi

export FZF_DEFAULT_OPTS_FILE=$HOME/.config/fzf/fzfrc
export ZSH_TMUX_CONFIG="$HOME/.config/tmux/tmux.conf"

# ============================================================================
# ZINIT CONFIGURATION
# ============================================================================

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Plugin options
zstyle ':omz:plugins:alias-finder' autoload yes
zstyle ':omz:plugins:alias-finder' exact yes
zstyle ':omz:plugins:eza' 'color-scale' all
zstyle ':omz:plugins:eza' 'dirs-first' yes
zstyle ':omz:plugins:eza' 'git-status' yes
zstyle ':omz:plugins:eza' 'header' yes
zstyle ':omz:plugins:eza' 'icons' yes
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:(cd|eza):*' fzf-preview '[[ -d $realpath ]] && eza -1 --color=always --icons $realpath || bat --theme=Dracula --color=always --style=plain --line-range=:250 $realpath'
zstyle ':fzf-tab:*' fzf-flags --height=30%

ZSH_AUTOSUGGEST_STRATEGY=completion
ZVM_VI_HIGHLIGHT_FOREGROUND=#82AAFF
ZVM_VI_HIGHLIGHT_BACKGROUND=#636DA6

# Annexes
zinit light-mode for \
  zdharma-continuum/zinit-annex-as-monitor \
  zdharma-continuum/zinit-annex-bin-gem-node \
  zdharma-continuum/zinit-annex-patch-dl \
  zdharma-continuum/zinit-annex-rust

# Prompt theme
zinit ice depth"1"
zinit light romkatv/powerlevel10k

# Plugins
zinit wait lucid for \
  OMZL::clipboard.zsh \
  OMZL::compfix.zsh \
  OMZL::directories.zsh \
  OMZL::functions.zsh \
  OMZL::git.zsh \
  OMZL::grep.zsh \
  OMZL::history.zsh \
  OMZL::key-bindings.zsh \
  OMZL::misc.zsh \
  OMZL::termsupport.zsh \
  OMZL::theme-and-appearance.zsh \
  OMZP::alias-finder \
  OMZP::aliases \
  OMZP::azure \
  OMZP::colored-man-pages \
  OMZP::command-not-found \
  OMZP::docker \
  OMZP::docker-compose \
  OMZP::eza \
  OMZP::gh \
  OMZP::git \
  OMZP::helm \
  OMZP::jsontools \
  OMZP::kubectl \
  OMZP::minikube \
  OMZP::npm \
  OMZP::pip \
  OMZP::python \
  OMZP::rails \
  OMZP::rake \
  OMZP::ruby \
  OMZP::ssh \
  OMZP::tailscale \
  OMZP::taskwarrior \
  OMZP::tmux \
  OMZP::web-search \
  OMZP::yarn \
  OMZP::z \
    as"completion" \
  OMZ::plugins/z/_z \
  Aloxaf/fzf-tab \
  zdharma-continuum/history-search-multi-word \
  rswrz/timewarrior-omz \
    blockf \
  zsh-users/zsh-autosuggestions \
  zsh-users/zsh-completions \
    atload"!_zsh_autosuggest_start" \
  zsh-users/zsh-history-substring-search \
  jeffreytse/zsh-vi-mode

# Syntax highlighting - loads last with compinit, triggers re-highlight on load
zinit wait"0b" lucid atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
  atload"_zsh_highlight" for \
  zdharma-continuum/fast-syntax-highlighting

zinit wait"0c" lucid for \
    atload"replace_plugin_aliases" \
    zdharma-continuum/null

# ============================================================================
# ZSH OPTIONS & KEYBINDINGS
# ============================================================================

insert-linebreak () {
  LBUFFER+=\ \\$'\n'
}
zle -N insert-linebreak

bindkey -v
bindkey '^x' clear-screen
bindkey '^h' vi-backward-char
bindkey '^l' vi-forward-char
bindkey "^[^M" insert-linebreak
bindkey -M vicmd ';' edit-command-line
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# ============================================================================
# ALIASES
# ============================================================================

replace_plugin_aliases() {
  alias g="lazygit"
  alias y="yadm enter lazygit"
  unalias ya 2>/dev/null
}

alias vim="nvim"
e() {
  "$EDITOR" "$@"
}

if (( $+commands[bat] )); then
  alias cat="bat --theme=Dracula"
elif (( $+commands[batcat] )); then
  alias cat="batcat"
fi

alias ll='ls -la --total-size'
alias la='ls -a'
alias lt='ls --tree'

alias j="just"
alias jl="just --list"
alias ju="just update"

alias d="lazydocker"

alias watch="viddy -d -n 1 --shell zsh "$@""

if (( $+commands[lynx] )); then
  alias lynx="lynx -cfg=$HOME/.config/lynx/lynx.cfg -lss=$HOME/.config/lynx/lynx.lss"
fi

if (( $+commands[task] )) && (( $+commands[timew] )); then
  tsync() {
    local uuid=$1

    if [[ -z "$uuid" ]]; then
      echo "Usage: sync_task <uuid> [start] [end]"
      return 1
    fi

    local desc=$(task _get "$uuid.description")
    local proj=$(task _get "$uuid.project")
    local tags=$(task _get "$uuid.tags" | tr ',' ' ')

    local start="${2:-$(task _get "$uuid.start")}"
    local end="${3:-$(task _get "$uuid.end")}"

    [[ -z "$desc" ]] && { echo "Error: Task '$uuid' not found."; return 1; }

    local timew_args=("$desc")
    [[ -n "$proj" ]] && timew_args+=("$proj")
    [[ -n "$tags" ]] && timew_args+=(${=tags})

    if [[ -n "$start" && -n "$end" ]]; then
      echo "Tracking (DOM/Args): $start - $end"
      timew track "$start" - "$end" "${timew_args[@]}"
    elif [[ -n "$start" ]]; then
      echo "Starting timer at: $start"
      timew start "$start" "${timew_args[@]}"
    else
      echo "No dates found. Starting live timer now..."
      timew start "${timew_args[@]}"
    fi
  }
fi

# Yazi with directory change on exit
x() {
  local tmp cwd target

  tmp="$(mktemp -t "yazi-cwd.XXXXXX")" || return 1

  if (( $# > 0 )); then
    target="${~*}"
    if [[ ! -d $target ]]; then
      target="$(z -e -- "$@")" || target="${~*}"
    fi
  fi

  yazi ${target:+"$target"} --cwd-file="$tmp"

  [[ -s $tmp ]] && cwd="$(<"$tmp")" && [[ $cwd != "$PWD" ]] && builtin cd -- "$cwd"

  rm -f -- "$tmp"
}

# Z with fzf integration
zin() {
  if (( ! $+commands[nvim] )); then
    echo "z not found"
    return 1
  fi
  local dir
  dir=$(z | awk '{$1=""; sub(/^ +/, ""); print }' | fzf --tac --preview 'eza -gh --git --icons=auto --color=always --color-scale=all --tree "$(echo {})" | head -80')
  [[ -n "$dir" ]] && cd "$dir"
}

# VisiData pip wrapper
vdpip() {
  if (( ! $+commands[visidata] )); then
    echo "Visidata not found."
    return 1
  fi

  local vdp_python
  if [[ -n "$BREW_PREFIX" ]]; then
    vdp_python="$BREW_PREFIX/opt/visidata/libexec/bin/python3"
  else
    vdp_python="$( (( ! $+commands[visidata] )) | xargs dirname)/../libexec/bin/python3"
  fi

  if [[ ! -x "$vdp_python" ]]; then
    echo "Embedded Python not found at: $vdp_python"
    return 1
  fi

  "$vdp_python" -m pip "$@"
}

fuck() {
  unfunction fuck
  eval $(thefuck --alias)
  fuck "$@"
}

# ============================================================================
# LOAD ITERM2 SHELL INTEGRATION
# ============================================================================

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# ============================================================================
# PROMPT CONFIGURATION
# ============================================================================

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# ============================================================================
# SOURCE ADDITIONAL SCRIPTS
# ============================================================================

scripts_dir="${0:A:h}/zshrc.os"

if [[ -d "$scripts_dir" ]]; then
  setopt null_glob
  for script in "$scripts_dir"/*.sh; do
    [[ "$script" == *"##"* ]] && continue
    [[ -r "$script" ]] && source "$script"
  done
  unsetopt null_glob
fi
