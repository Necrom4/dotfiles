# ============================================================================
# INSTANT PROMPT (Anything below will run in the background)
# ============================================================================

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ============================================================================
# ENVIRONMENT VARIABLES
# ============================================================================

if [[ -x /opt/homebrew/bin/brew ]]; then
  BREW_PREFIX="/opt/homebrew"
elif [[ -x /usr/local/bin/brew ]]; then
  BREW_PREFIX="/usr/local"
else
  BREW_PREFIX=""
fi

typeset -U PATH path

if [[ -n "$BREW_PREFIX" ]]; then
  path=(
    "$BREW_PREFIX/opt/postgresql@18/bin"
    "$BREW_PREFIX/share/john"
    $path
  )
  export LDFLAGS="-L$BREW_PREFIX/opt/postgresql@18/lib"
  export CPPFLAGS="-I$BREW_PREFIX/opt/postgresql@18/include"
fi

path+=("$HOME/.local/bin")

if (( $+commands[nvim] )); then
  export EDITOR="nvim"
elif (( $+commands[vim] )); then
  export EDITOR="vim"
else
  export EDITOR="vi"
fi

export FZF_DEFAULT_OPTS_FILE=$HOME/.config/fzf/fzfrc
export ZSH_TMUX_CONFIG="$HOME/.config/tmux/tmux.conf"

# Add $PR var that represents the path of the current project's root
function update_project_root_var() {
  local dir="$PWD"
  while [[ "$dir" != "/" ]]; do
      if [[ -d "$dir/.git" ]]; then
          export PR="$dir"
          return
      fi
      dir="${dir:h}"
  done
  unset PR
}

autoload -Uz add-zsh-hook
add-zsh-hook chpwd update_project_root_var

update_project_root_var

# ============================================================================
# ZINIT CONFIGURATION
# ============================================================================

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Plugin options
zstyle ':omz:plugins:alias-finder' autoload yes
zstyle ':omz:plugins:alias-finder' exact yes
zstyle ':omz:plugins:eza' 'color-scale' all
zstyle ':omz:plugins:eza' 'dirs-first' yes
zstyle ':omz:plugins:eza' 'git-status' yes
zstyle ':omz:plugins:eza' 'header' yes
zstyle ':omz:plugins:eza' 'icons' yes
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' menu no
zstyle ':fzf-tab:*' fzf-flags --height=30%
zstyle ':fzf-tab:complete:(cd|eza):*' fzf-preview '[[ -d $realpath ]] && eza -1 --color=always --icons $realpath || bat --color=always --style=plain --line-range=:250 $realpath'
zstyle ':fzf-tab:complete:(z|zshz):*' fzf-preview 'eza -L 3 -gh --git --icons=auto --color=always --color-scale=all --tree $word | head -80'
zstyle ':fzf-tab:complete:brew-(info|install|upgrade):*' fzf-preview 'F=( -c %Y ); [[ $(uname) == Darwin ]] && F=( -f %m ); CACHE="/tmp/brew-info-$word"; [[ -f $CACHE && $((EPOCHSECONDS - $(stat $F $CACHE))) -lt 86400 ]] || HOMEBREW_COLOR=1 brew info $word 2>/dev/null >$CACHE; cat $CACHE'
zstyle ':fzf-tab:complete:just:*' fzf-preview \
  'just --show $word 2>/dev/null | bat --color=always --style=plain -l Makefile'

ZSH_AUTOSUGGEST_STRATEGY=(history completion)
ZVM_VI_HIGHLIGHT_FOREGROUND=#82AAFF
ZVM_VI_HIGHLIGHT_BACKGROUND=#636DA6

# Annexes
zinit light-mode for \
  zdharma-continuum/zinit-annex-as-monitor \
  zdharma-continuum/zinit-annex-bin-gem-node \
  zdharma-continuum/zinit-annex-patch-dl \
  zdharma-continuum/zinit-annex-rust

# Prompt theme
zinit ice depth"1"
zinit light romkatv/powerlevel10k

# Plugins necessary instantly + completion providers
zinit wait lucid for \
  OMZL::compfix.zsh \
  OMZL::directories.zsh \
  OMZL::history.zsh \
  OMZL::key-bindings.zsh \
  OMZP::ansible \
  OMZP::argocd \
  OMZP::azure \
  OMZP::cp \
  OMZP::docker \
  OMZP::docker-compose \
    atload'unalias x 2>/dev/null' \
  OMZP::extract \
  OMZP::gem \
  OMZP::gh \
  OMZP::git \
  OMZP::helm \
  OMZP::k9s \
  OMZP::kubectl \
  OMZP::minikube \
  OMZP::npm \
  OMZP::pip \
  OMZP::python \
  OMZP::rake \
  OMZP::rsync \
  OMZP::ruby \
  OMZP::ssh \
  OMZP::tailscale \
  OMZP::taskwarrior \
    atclone'curl -sLo tmux.extra.conf https://github.com/ohmyzsh/ohmyzsh/raw/master/plugins/tmux/tmux.extra.conf' \
    atpull'%atclone' \
  OMZP::tmux \
    atload'unalias ya 2>/dev/null' \
  OMZP::yarn \
  OMZP::z \
    as"completion" \
  OMZ::plugins/z/_z \
  Aloxaf/fzf-tab \
  rswrz/timewarrior-omz \
    blockf \
  zsh-users/zsh-autosuggestions \
  zsh-users/zsh-completions \
    atload"!_zsh_autosuggest_start" \
  zsh-users/zsh-history-substring-search \
  jeffreytse/zsh-vi-mode

# Syntax highlighting - loads with compinit, triggers re-highlight on load
zinit wait"0b" lucid atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
  atload"_zsh_highlight" for \
  zdharma-continuum/fast-syntax-highlighting

# Deferred plugins
zinit wait"1" lucid for \
  OMZL::clipboard.zsh \
  OMZL::completion.zsh \
  OMZL::functions.zsh \
  OMZL::grep.zsh \
  OMZL::misc.zsh \
  OMZL::spectrum.zsh \
  OMZL::termsupport.zsh \
  OMZP::alias-finder \
    atclone'
      curl -sLo cheatsheet.py https://raw.githubusercontent.com/ohmyzsh/ohmyzsh/master/plugins/aliases/cheatsheet.py;
      curl -sLo termcolor.py https://raw.githubusercontent.com/termcolor/termcolor/1.1.0/termcolor.py' \
    atpull'%atclone' \
  OMZP::aliases \
  OMZP::colored-man-pages \
  OMZP::command-not-found \
  OMZP::eza \
  OMZP::grc \
  OMZP::jsontools \
  OMZP::rails \
  OMZP::web-search \
  zdharma-continuum/history-search-multi-word

# Override plugin aliases
zinit wait"1b" lucid for \
    atload"override_aliases" \
    zdharma-continuum/null

# ============================================================================
# ZSH OPTIONS & KEYBINDINGS
# ============================================================================

insert-linebreak () {
  LBUFFER+=\ \\$'\n'
}
zle -N insert-linebreak

bindkey -v
bindkey '^x' clear-screen
bindkey '^h' vi-backward-char
bindkey '^l' vi-forward-char
bindkey "^[^M" insert-linebreak
bindkey -M vicmd ';' edit-command-line
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# ============================================================================
# ALIASES
# ============================================================================

override_aliases() {
  alias g="lazygit"
  alias y="yadm enter lazygit"
}

alias vim="nvim"
e() {
  "$EDITOR" "$@"
}

if (( $+commands[bat] )); then
  alias cat="bat"
fi

alias ll='ls -la --total-size'
alias la='ls -a'
alias lt='ls --tree'

alias j="just"
alias jl="just --list"

alias d="lazydocker"
alias g="lazygit"
alias y="yadm enter lazygit"

alias watch="viddy -d -n 1 --shell zsh "$@""

if (( $+commands[lynx] )); then
  alias lynx="lynx -cfg=$HOME/.config/lynx/lynx.cfg -lss=$HOME/.config/lynx/lynx.lss"
fi

if (( $+commands[task] )) && (( $+commands[timew] )); then
  tsync() {
    local uuid=$1

    if [[ -z "$uuid" ]]; then
      echo "Usage: sync_task <uuid> [start] [end]"
      return 1
    fi

    local desc=$(task _get "$uuid.description")
    local proj=$(task _get "$uuid.project")
    local tags=$(task _get "$uuid.tags" | tr ',' ' ')

    local start="${2:-$(task _get "$uuid.start")}"
    local end="${3:-$(task _get "$uuid.end")}"

    [[ -z "$desc" ]] && { echo "Error: Task '$uuid' not found."; return 1; }

    local timew_args=("$desc")
    [[ -n "$proj" ]] && timew_args+=("$proj")
    [[ -n "$tags" ]] && timew_args+=(${=tags})

    if [[ -n "$start" && -n "$end" ]]; then
      echo "Tracking (DOM/Args): $start - $end"
      timew track "$start" - "$end" "${timew_args[@]}"
    elif [[ -n "$start" ]]; then
      echo "Starting timer at: $start"
      timew start "$start" "${timew_args[@]}"
    else
      echo "No dates found. Starting live timer now..."
      timew start "${timew_args[@]}"
    fi
  }
fi

# Yazi with directory change on exit
x() {
  local tmp cwd target

  tmp="$(mktemp -t "yazi-cwd.XXXXXX")" || return 1

  if (( $# > 0 )); then
    target="${~*}"
    if [[ ! -d $target ]]; then
      target="$(z -e -- "$@")" || target="${~*}"
    fi
  fi

  yazi ${target:+"$target"} --cwd-file="$tmp"

  [[ -s $tmp ]] && cwd="$(<"$tmp")" && [[ $cwd != "$PWD" ]] && builtin cd -- "$cwd"

  rm -f -- "$tmp"
}

# VisiData pip wrapper
vdpip() {
  if (( ! $+commands[visidata] )); then
    echo "Visidata not found."
    return 1
  fi

  local vdp_python
  if [[ -n "$BREW_PREFIX" ]]; then
    vdp_python="$BREW_PREFIX/opt/visidata/libexec/bin/python3"
  else
    vdp_python="$( (( ! $+commands[visidata] )) | xargs dirname)/../libexec/bin/python3"
  fi

  if [[ ! -x "$vdp_python" ]]; then
    echo "Embedded Python not found at: $vdp_python"
    return 1
  fi

  "$vdp_python" -m pip "$@"
}

fuck() {
  unfunction fuck
  eval $(thefuck --alias)
  fuck "$@"
}

# FZF a word inside all in the current path
rg-fzf() {
  RG_PREFIX="rg --files-with-matches --smart-case"
  # Pass paths to use or use `.`
  local search_path="${1:-.}"
  # We store the search query in a variable so we can use it after fzf exits
  # We use --print-query so fzf returns two lines: 1. Your typed string, 2. The selected file
  local out
  out=$(
    # Run the initial command against the search_path
    FZF_DEFAULT_COMMAND="$RG_PREFIX '' '$search_path'" \
      fzf --ansi \
      --sort \
      --phony \
      --bind "change:reload:$RG_PREFIX {q} '$search_path'" \
      --preview="[[ ! -z {} ]] && rg --pretty --context 5 {q} {}" \
      --preview-window="70%:wrap" \
      --print-query
    )

  local query=$(echo "$out" | head -n 1)
  if [ -z "$query" ]; then return; fi

  rg --line-number "$query" "$search_path"
}

# ============================================================================
# LOAD ITERM2 SHELL INTEGRATION
# ============================================================================

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# ============================================================================
# PROMPT CONFIGURATION
# ============================================================================

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# ============================================================================
# SOURCE ADDITIONAL SCRIPTS
# ============================================================================

scripts_dir="${0:A:h}/zshrc.os"

if [[ -d "$scripts_dir" ]]; then
  setopt null_glob
  for script in "$scripts_dir"/*.sh "$scripts_dir"/*.zsh; do
    [[ "$script" == *"##"* ]] && continue
    [[ -r "$script" ]] && source "$script"
  done
  unsetopt null_glob
fi
