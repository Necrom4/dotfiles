# ============================================================================
# FASTFETCH (displays system info on startup)
# ============================================================================

# if [[ -o interactive ]] && [[ -z "$_ZSHRC_SOURCED" ]]; then
#   if (( $+commands[fastfetch] )); then
#     fastfetch
#   fi
#   export _ZSHRC_SOURCED=1
# fi

# ============================================================================
# INSTANT PROMPT (Anything below will run in the background)
# ============================================================================

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ============================================================================
# ENVIRONMENT VARIABLES
# ============================================================================

if (( $+commands[brew] )); then
  BREW_PREFIX=$(brew --prefix)
elif [[ -x /usr/local/bin/brew ]]; then
  BREW_PREFIX="/usr/local"
elif [[ -x /opt/homebrew/bin/brew ]]; then
  BREW_PREFIX="/opt/homebrew"
else
  BREW_PREFIX=""
fi

if [[ -n "$BREW_PREFIX" ]]; then
  export PATH="$BREW_PREFIX/share/john:$PATH"
  export PATH="$BREW_PREFIX/opt/postgresql@18/bin:$PATH"
  export LDFLAGS="-L$BREW_PREFIX/opt/postgresql@18/lib"
  export CPPFLAGS="-I$BREW_PREFIX/opt/postgresql@18/include"
fi

if (( $+commands[nvim] )); then
  export EDITOR="nvim"
elif (( $+commands[vim] )); then
  export EDITOR="vim"
else
  export EDITOR="vi"
fi

export ZSH_TMUX_CONFIG="$HOME/.config/tmux/tmux.conf"
# Created by `pipx` on 2025-12-30 17:17:47
export PATH="$PATH:/Users/diogopombeiro/.local/bin"

# ============================================================================
# ZINIT CONFIGURATION
# ============================================================================

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# omz plugins options
zstyle ':omz:plugins:alias-finder' autoload yes
zstyle ':omz:plugins:alias-finder' exact yes
zstyle ':omz:plugins:eza' 'color-scale' all
zstyle ':omz:plugins:eza' 'dirs-first' yes
zstyle ':omz:plugins:eza' 'git-status' yes
zstyle ':omz:plugins:eza' 'header' yes
zstyle ':omz:plugins:eza' 'icons' yes

# Load annexes without Turbo (required for annexes)
zinit light-mode for \
  zdharma-continuum/zinit-annex-as-monitor \
  zdharma-continuum/zinit-annex-bin-gem-node \
  zdharma-continuum/zinit-annex-patch-dl \
  zdharma-continuum/zinit-annex-rust

# zsh-vi-mode config (must be set BEFORE plugin loads)
ZVM_VI_HIGHLIGHT_FOREGROUND=#82AAFF
ZVM_VI_HIGHLIGHT_BACKGROUND=#636DA6
# Fix for fast-syntax-highlighting: re-source FSH after zvm completes init
zvm_after_init_commands+=('[ -f "${ZINIT[PLUGINS_DIR]}/zdharma-continuum---fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh" ] && source "${ZINIT[PLUGINS_DIR]}/zdharma-continuum---fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh"')

# Theme - no turbo, needed for instant prompt
zinit ice depth"1"
zinit light romkatv/powerlevel10k

# OMZ libs - load early (wait"0") as they're dependencies
zinit wait"0" lucid for \
  OMZL::clipboard.zsh \
  OMZL::compfix.zsh \
  OMZL::completion.zsh \
  OMZL::directories.zsh \
  OMZL::functions.zsh \
  OMZL::git.zsh \
  OMZL::grep.zsh \
  OMZL::history.zsh \
  OMZL::key-bindings.zsh \
  OMZL::misc.zsh \
  OMZL::spectrum.zsh \
  OMZL::termsupport.zsh \
  OMZL::theme-and-appearance.zsh

# Completions - load early with blockf to prevent compinit
zinit wait"0b" lucid blockf for \
  zsh-users/zsh-completions

# Initialize completions
zinit wait"0c" lucid atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" for \
  zdharma-continuum/null

# OMZ plugins - staggered loading
zinit wait"1" lucid for \
  OMZP::alias-finder \
  OMZP::aliases \
  OMZP::colored-man-pages \
  OMZP::command-not-found \
  OMZP::docker \
  OMZP::docker-compose \
  OMZP::eza \
  OMZP::gh \
  OMZP::git \
  OMZP::jsontools \
  OMZP::npm \
  OMZP::pip \
  OMZP::python \
  OMZP::rails \
  OMZP::rake \
  OMZP::ruby \
  OMZP::ssh \
  OMZP::web-search \
  OMZP::yarn \
  OMZP::z

# Heavier OMZ plugins - load slightly later
zinit wait"2" lucid for \
  OMZP::azure \
  OMZP::helm \
  OMZP::kubectl \
  OMZP::minikube \
  OMZP::tailscale \
  OMZP::taskwarrior \
  OMZP::tmux

# Emoji plugin - load without turbo due to multibyte requirement
# zinit lucid for \
#   atload"setopt multibyte" \
#   OMZP::emoji

# Other plugins
zinit wait"1" lucid for \
  rswrz/timewarrior-omz

# Autosuggestions
zinit wait"0b" lucid atload"!_zsh_autosuggest_start" for \
  zsh-users/zsh-autosuggestions \
  zsh-users/zsh-history-substring-search

# History search
zinit wait"1" lucid for \
  zdharma-continuum/history-search-multi-word

# Vi-mode - load with callback for FSH integration
zinit wait"0a" lucid for \
  jeffreytse/zsh-vi-mode

# Syntax highlighting - MUST load last, after vi-mode
# Use zvm_after_init to ensure it loads after vi-mode completes widget setup
zinit wait"0c" lucid atload"
  # Reinitialize FSH after vi-mode to fix highlighting while typing
  if (( \$+functions[_zsh_highlight] )); then
    _zsh_highlight
  fi
" for \
  zdharma-continuum/fast-syntax-highlighting

# ============================================================================
# ZSH OPTIONS & KEYBINDINGS
# ============================================================================

insert-linebreak () {
  LBUFFER+=\ \\$'\n'
}
zle -N insert-linebreak

bindkey -v
bindkey '^x' clear-screen
bindkey '^h' vi-backward-char
bindkey '^l' vi-forward-char
bindkey "^[^M" insert-linebreak
bindkey -M vicmd ';' edit-command-line
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# ============================================================================
# ALIASES
# ============================================================================

unalias ya 2>/dev/null

alias vim="nvim"
e() {
  "$EDITOR" "$@"
}

if (( $+commands[bat] )); then
  alias cat="bat --theme=Dracula"
elif (( $+commands[batcat] )); then
  alias cat="batcat"
fi

alias ll='ls -la --total-size'
alias la='ls -a'
alias lt='ls --tree'

alias j="just"
alias jl="just --list"
alias ju="just update"

alias d="lazydocker"
alias g="lazygit"
alias y="yadm enter lazygit"

alias watch="viddy -d -n 1 --shell zsh "$@""

if (( $+commands[lynx] )); then
  alias lynx="lynx -cfg=$HOME/.config/lynx/lynx.cfg -lss=$HOME/.config/lynx/lynx.lss"
fi

if (( $+commands[task] )) && (( $+commands[timew] )); then
  tsync() {
    local uuid=$1

    if [[ -z "$uuid" ]]; then
      echo "Usage: sync_task <uuid> [start] [end]"
      return 1
    fi

    local desc=$(task _get "$uuid.description")
    local proj=$(task _get "$uuid.project")
    local tags=$(task _get "$uuid.tags" | tr ',' ' ')

    local start="${2:-$(task _get "$uuid.start")}"
    local end="${3:-$(task _get "$uuid.end")}"

    [[ -z "$desc" ]] && { echo "Error: Task '$uuid' not found."; return 1; }

    local timew_args=("$desc")
    [[ -n "$proj" ]] && timew_args+=("$proj")
    [[ -n "$tags" ]] && timew_args+=(${=tags})

    if [[ -n "$start" && -n "$end" ]]; then
      echo "Tracking (DOM/Args): $start - $end"
      timew track "$start" - "$end" "${timew_args[@]}"
    elif [[ -n "$start" ]]; then
      echo "Starting timer at: $start"
      timew start "$start" "${timew_args[@]}"
    else
      echo "No dates found. Starting live timer now..."
      timew start "${timew_args[@]}"
    fi
  }
fi

if [ "$(yadm config local.class)" = "Personal" ]; then
  alias vpnify="/Applications/Vpnify.app/Contents/MacOS/CLI/vpnify"
fi

if [ "$(yadm config local.class)" = "42" ]; then
  alias cclean='bash ~/LinuxCleaner_42.sh'
fi

# Yazi with directory change on exit
x() {
  local tmp cwd target

  tmp="$(mktemp -t "yazi-cwd.XXXXXX")" || return 1

  if (( $# > 0 )); then
    target="${~*}"
    if [[ ! -d $target ]]; then
      target="$(z -e -- "$@")" || target="${~*}"
    fi
  fi

  yazi ${target:+"$target"} --cwd-file="$tmp"

  [[ -s $tmp ]] && cwd="$(<"$tmp")" && [[ $cwd != "$PWD" ]] && builtin cd -- "$cwd"

  rm -f -- "$tmp"
}

# Z with fzf integration
zin() {
  if ! command -v z &>/dev/null; then
    echo "z not found"
    return 1
  fi
  local dir
  dir=$(z | awk '{$1=""; sub(/^ +/, ""); print }' | fzf --tac --preview 'eza -gh --git --icons=auto --color=always --color-scale=all --tree "$(echo {})" | head -80')
  [[ -n "$dir" ]] && cd "$dir"
}

# VisiData pip wrapper
vdpip() {
  if ! command -v visidata &>/dev/null; then
    echo "Visidata not found."
    return 1
  fi

  local vdp_python
  if [[ -n "$BREW_PREFIX" ]]; then
    vdp_python="$BREW_PREFIX/opt/visidata/libexec/bin/python3"
  else
    vdp_python="$(command -v visidata | xargs dirname)/../libexec/bin/python3"
  fi

  if [[ ! -x "$vdp_python" ]]; then
    echo "Embedded Python not found at: $vdp_python"
    return 1
  fi

  "$vdp_python" -m pip "$@"
}

eval $(thefuck --alias)

# ============================================================================
# LOAD ITERM2 SHELL INTEGRATION
# ============================================================================

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# ============================================================================
# PROMPT CONFIGURATION
# ============================================================================

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# ============================================================================
# SOURCE ADDITIONAL SCRIPTS
# ============================================================================

scripts_dir="${0:A:h}/zshrc.os"

if [[ -d "$scripts_dir" ]]; then
  setopt null_glob
  for script in "$scripts_dir"/*.sh; do
    [[ "$script" == *"##"* ]] && continue
    [[ -r "$script" ]] && source "$script"
  done
  unsetopt null_glob
fi
